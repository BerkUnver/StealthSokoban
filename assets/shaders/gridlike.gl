#ifdef VERTEX_SHADER
layout(location=LOC_POSITION)      in vec3 vertex_position;
layout(location=LOC_UV)            in vec2 vertex_uv;
layout(location=LOC_VIEWED_OFFSET) in ivec3 vertex_viewed_offset;
layout(location=LOC_OUTLINE_UV)    in vec2 vertex_outline_uv;
#else
layout(location=0) out vec4 fragment_color;
#endif

uniform sampler2D bitmap;
uniform sampler2D outline_bitmap;
uniform mat4 projection;
uniform vec4 color;

#ifdef GAME
uniform usamplerBuffer viewed;
uniform ivec3 viewed_size;
uniform ivec3 viewed_position;
#endif


OUTIN vec2 fragment_uv;
OUTIN vec2 fragment_outline_uv;

#ifdef GAME
OUTIN float fragment_brightness;
#endif


// @Speed figure out how to optimize branching
void main() {
#ifdef VERTEX_SHADER
#ifdef GAME
    ivec3 viewed_coord = viewed_position + vertex_viewed_offset;
    
    int index;
    bool success = get_3d_index(viewed_coord, viewed_size, index);
        
    if (!success) {
        gl_Position = vec4(0, 0, -2, 1);
        return;
    }

    uint filled = texelFetch(viewed, index).r;

    if ((filled & VIEW_STATE_VISIBLE) != 0u) {
        fragment_brightness = 1;
        gl_Position = projection * vec4(vertex_position, 1.0);
    } else if ((filled & VIEW_STATE_SEEN) != 0u) {
        fragment_brightness = SEEN_BRIGHTNESS;
        gl_Position = projection * vec4(vertex_position, 1.0);
    } else {
        gl_Position = vec4(0, 0, -2, 1);
        return;
    }
#else 
    gl_Position = projection * vec4(vertex_position, 1.0);
#endif
    fragment_uv = vertex_uv;
    fragment_outline_uv = vertex_outline_uv;
#else
    vec4 outline_color = texture(outline_bitmap, fragment_outline_uv);
    vec4 bitmap_color = texture(bitmap, fragment_uv);
    
    vec4 combined_color = vec4(mix(bitmap_color.xyz, outline_color.xyz, outline_color.a), bitmap_color.a);
    vec4 tinted_color = color * combined_color;
#ifdef GAME
    fragment_color = vec4(tinted_color.rgb * fragment_brightness, tinted_color.a);
#else
    fragment_color = tinted_color;
#endif
#endif
}
